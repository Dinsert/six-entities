plugins {
    id 'org.springframework.boot'
    id 'org.openapi.generator'
}

openApiGenerate {
    generatorName = "spring"

    inputSpec = layout.projectDirectory
            .file("src/main/resources/openapi/openapi.yml")
            .asFile
            .path

    outputDir = layout.buildDirectory
            .dir("generated")
            .get()
            .asFile
            .path

    apiPackage = "com.example.six_entities.api"
    modelPackage = "com.example.six_entities.model"

    configOptions = [
            interfaceOnly  : "true",
            useSpringBoot3 : "true",
            useTags        : "true",
            openApiNullable: "false"
    ]
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory
                    .dir("generated/src/main/java")
                    .get()
                    .asFile
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.5"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.4"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation "org.mapstruct:mapstruct:1.5.5.Final"

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}